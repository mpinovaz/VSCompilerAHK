#Requires AutoHotkey v2.0
#SingleInstance Force

Compiler:= "Ahk2Exe.exe"
Main := DepuracionInicial()
SplitPath(Main, &ScriptAHK, &ScriptDir)
Proyecto := StrReplace(ScriptAHK, ".ahk", "")

;
;	Comprobamos si est√° definida RutaGeneral en VSCompilerAHK.cfg, tras lo cual y si es as√≠,
;	validamos que el proyecto reside en una Subcarpeta con su mismo nombre
;
RutaGral:= LeerCFG("RutaGeneral")
If ( RutaGral != "" && InStr(ScriptDir, RutaGral, False) ) {
    SubCarpGral := StrSplit(ScriptDir, "\")[-1]
    If SubCarpGral != Proyecto {
		EmitirMensaje( "üö´ Ruta bajo '" RutaGral "' no cumple convenci√≥n:`n" .
						"Subcarpeta '" SubCarpGral "' ‚â† Nombre del script '" Proyecto "'.`n" .
						"Cancelado.", 1, "VSC")
	}
}

;
;	Comprobamos que el EXE se obtiene correctamente
;
FechaInicio := A_Now
RunWait Format('"{1}" /in "{2}"', Compiler, Main), ScriptDir
RutaExe := ScriptDir "\" Proyecto ".exe"
if ( ! FileExist(RutaExe) ) {
    EmitirMensaje("‚ùå No se ha generado el archivo '" Proyecto ".exe'", 2, "VSC")
}
FechaExe := FileGetTime(RutaExe, "M")
if ( FechaExe < FechaInicio ) {
    EmitirMensaje("‚ùå El ejecutable no fue actualizado en esta compilaci√≥n.`n" .
				  "Fecha .exe: " FechaExe " vs Inicio: " FechaInicio, 2, "VSC")
}

;
;	√âxito, EXE obtenido
;
EmitirMensaje("‚úì Obtenido: " Chr(34) Proyecto Chr(34) ".exe en la carpeta " Chr(34) ScriptDir Chr(34), 0, "VSC")
ExitApp 0

;============================================
;	Leer clave del archivo VSCompile.cfg
;============================================
LeerCFG(clave) {
    archivo := A_ScriptDir "\VSCompilerAHK.cfg"
    return FileExist(archivo) ? IniRead(archivo, "", clave, "") : ""
}

;========================
;	Depuraci√≥n Inicial
;========================
DepuracionInicial() {
	If ( ! A_IsCompiled ) {			; Autocompilaci√≥n
		Autocompilacion()
	}

	If ( ! FileExist(Compiler) ) {	; Comprobamos que VSCompilerAHK.exe reside donde Ahk2Exe.exe
		EmitirMensaje("‚ùå No se encuentra el compilador " Compiler " junto a VSCompilerAHK.exe.`nCancelado.", 4)
	}

	if ( A_Args.Length < 1 ) {		; Comprobamos que la llamada a VSCompilerAHK.exe incluye par√°metro con la ruta del AHK a compilar
		EmitirMensaje("‚ùå VSCompilerAHK.exe no parece haber sido invocado desde VS Code.`nCancelado.", 3)
	}
	
	Main := A_Args[1]
	If ( ! FileExist(Main) ) {		; Comprobamos que el AHK a compilar existe (b√°sicamente que fue guardado)
		EmitirMensaje("‚ùå El archivo AHK no existe o a√∫n no fue guardado.`nCancelado.", 3, "VSC")
	}
	Return Main
}

;=========================================
;	Autocompilaci√≥n
;=========================================
Autocompilacion() {
	Nombre    := "VSCompilerAHK"
	NombreAHK := Nombre ".ahk"
	NombreEXE := Nombre ".exe"
	
	if ! FileExist(Compiler) {
		EmitirMensaje( "üö´ Has de disponer " NombreAHK " donde resida " Compiler ".`nCancelado", 10)
	}
	
	If FileExist(NombreEXE) {
		EmitirMensaje( "üö´ " NombreAHK " est√° pensado para ser ejecutado si no existe " NombreEXE ".`nCancelado", 10)
	}
	
	Tooltip "Generando " NombreEXE "..."
	RunWait(Compiler " /in " NombreAHK)
	Tooltip
	
	FileExist(NombreEXE) ? EmitirMensaje( "‚úì " NombreEXE " generado correctamente.", 0)
						 : EmitirMensaje("üö´ Error al generar " NombreEXE, 1)
}

;===============================================================
;	Mensaje normal por MsgBox o Consola de VS Code seg√∫n Tipo
;===============================================================
EmitirMensaje(Mensaje, Error:= 0, Tipo:= "Normal") {
	If ( Tipo == "Normal" ) {
		MsgBox(Mensaje)
	} Else {
		Mensaje := StrSplit(Mensaje,"`n")
		For _,Linea In Mensaje {
			FileAppend(Linea "`n", "*", "UTF-8")
		}
	}
	ExitApp Error
}
