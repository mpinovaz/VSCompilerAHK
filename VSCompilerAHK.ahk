#Requires AutoHotkey v2.0
#SingleInstance Force

;
;	Autocompilaci√≥n
;
Compiler:= "Ahk2Exe.exe"
If ( ! A_IsCompiled ) {
	Autocompilacion()
}

;
;	Comprobamos que VSCompilerAHK.exe reside donde Ahk2Exe.exe
;
If ( ! FileExist(Compiler) ) {
	ConsolaVSC("‚ùå No se encuentra el compilador " Compiler " junto a VSCompilerAHK.exe.`nCancelado.", 4)
}

;
;	Comprobamos que la llamada a VSCompilerAHK.exe incluye par√°metro con la ruta del AHK a compilar
;
if ( A_Args.Length < 1 ) {
	MsgBox("‚ùå VSCompilerAHK.exe no parece haber sido invocado desde VS Code.`nCancelado.")
	Exitapp 3
}

;
;	Comprobamos que el AHK a compilar existe (b√°sicamente que fue guardado)
;
Main := A_Args[1]
If ( ! FileExist(Main) ) {
	ConsolaVSC("‚ùå El archivo AHK no existe o a√∫n no fue guardado.`nCancelado.", 3)
}
SplitPath(Main, &ScriptAHK, &ScriptDir)
Proyecto := StrReplace(ScriptAHK, ".ahk", "")

;
;	Comprobamos si est√° definida RutaGeneral en VSCompilerAHK.cfg, tras lo cual y si es as√≠,
;	validamos que el proyecto reside en una Subcarpeta con su mismo nombre
;
RutaGral:= LeerCFG("RutaGeneral")
If ( RutaGral != "" && InStr(ScriptDir, RutaGral, False) ) {
    SubCarpGral := StrSplit(ScriptDir, "\")[-1]
    If SubCarpGral != Proyecto {
		ConsolaVSC( "üö´ Ruta bajo '" RutaGral "' no cumple convenci√≥n:`n" .
					"Subcarpeta '" SubCarpGral "' ‚â† Nombre del script '" Proyecto "'.`n" .
					"Cancelado.", 1)
	}
}

;
;	Comprobamos que el EXE se obtiene correctamente
;
FechaInicio := A_Now
RunWait Format('"{1}" /in "{2}"', Compiler, Main), ScriptDir
RutaExe := ScriptDir "\" Proyecto ".exe"
if ( ! FileExist(RutaExe) ) {
    ConsolaVSC("‚ùå No se ha generado el archivo '" Proyecto ".exe'", 2)
}
FechaExe := FileGetTime(RutaExe, "M")
if ( FechaExe < FechaInicio ) {
    ConsolaVSC("‚ùå El ejecutable no fue actualizado en esta compilaci√≥n.`n" .
               "Fecha .exe: " FechaExe " vs Inicio: " FechaInicio, 2)
}

;
;	√âxito, EXE obtenido
;
ConsolaVSC("‚úì Obtenido: " Chr(34) Proyecto Chr(34) ".exe en la carpeta " Chr(34) ScriptDir Chr(34))
ExitApp 0

;=================================================
;	Mensaje en la Consola de Visual Studio Code
;=================================================
ConsolaVSC(Mensaje, Error:= 0) {
	Mensaje := StrSplit(Mensaje,"`n")
	For _,Linea In Mensaje {
		FileAppend(Linea "`n", "*", "UTF-8")
	}
	If Error {
		Exitapp Error
	}
}

;============================================
;	Leer clave del archivo VSCompile.cfg
;============================================
LeerCFG(clave) {
    archivo := A_ScriptDir "\VSCompilerAHK.cfg"
    return FileExist(archivo) ? IniRead(archivo, "", clave, "") : ""
}
;=========================================
;	Autocompilaci√≥n
;=========================================
Autocompilacion() {
	Nombre    := "VSCompilerAHK"
	NombreAHK := Nombre ".ahk"
	NombreEXE := Nombre ".exe"
	if ! FileExist(Compiler) {
		MsgBox( "Has de disponer " NombreAHK " donde resida " Compiler ".`nCancelado")
		ExitApp 10
	}
	If FileExist(NombreEXE) {
		MsgBox( NombreAHK " est√° pensado para ser ejecutado si no existe " NombreEXE ".`nCancelado")
		ExitApp 10
	}
	Tooltip "Generando " NombreEXE "..."
	RunWait(Compiler " /in " NombreAHK)
	Tooltip
	if FileExist(NombreEXE) {
		MsgBox NombreEXE " generado correctamente."
		ExitApp 0
	} else {
		MsgBox "Error al generar " NombreEXE
		ExitApp 1
	}
}
